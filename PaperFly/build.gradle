buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.1'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

android {
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 18
    buildToolsVersion "18.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 18
    }

    signingConfigs {
        release {
            def Properties props = new Properties()
            def propFile = file('signing.properties')
            if (propFile.canRead()) {
                props.load(new FileInputStream(propFile))

                if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
                    println 'using signing.properties'
                    storeFile file(props['STORE_FILE'])
                    storePassword props['STORE_PASSWORD']
                    keyAlias props['KEY_ALIAS']
                    keyPassword props['KEY_PASSWORD']
                } else {
                    println 'signing.properties found but some entries are missing'
                    android.buildTypes.release.signingConfig = null
                }
            } else {
                println 'signing.properties not found'
                android.buildTypes.release.signingConfig = null
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

def jersey_version = '1.11.1'
def autobahn_version = '0.5.2-SNAPSHOT'
def gson_version = '2.2.4'

dependencies {
    compile 'com.android.support:appcompat-v7:18.0.0'
    compile 'com.android.support:support-v4:13.0.0'
    compile 'com.google.zxing:core:2.2'
    compile 'de.tavendo:autobahn-android:' + autobahn_version
    compile 'com.google.android.gms:play-services:3.1.36'
    compile 'org.projectlombok:lombok:1.12.2'
    compile 'com.sun.jersey.contribs.jersey-oauth:oauth-client:' + jersey_version
    compile 'com.sun.jersey.contribs.jersey-oauth:oauth-signature:' + jersey_version
    compile 'com.google.code.gson:gson:' + gson_version
}